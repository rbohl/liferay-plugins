import org.apache.commons.codec.digest.DigestUtils

apply from: "${gradle.lfrSdkDir}/versions.gradle"

buildscript {
	dependencies {
		classpath group: "commons-codec", name: "commons-codec", version: "1.9"
	}

	repositories {
		maven {
			url "http://cdn.repository.liferay.com/nexus/content/groups/public"
		}
	}
}

configure(allprojects) {
	apply from: "${gradle.lfrSdkDir}/util.gradle"

	configurations {
		all {
			resolutionStrategy.cacheChangingModulesFor 15, 'minutes'
		}
	}

	repositories {
		maven {
			url "http://cdn.repository.liferay.com/nexus/content/groups/public"
		}
	}
}

configure(
	subprojects.findAll {
		it.exists("build.gradle")
	}) {

	Closure readConfigurations
	Closure setExtProperties

	readConfigurations = {

		// bnd.bnd

		project.ext.bndProperties = new Properties()

		File bndPropertiesFile = project.file("bnd.bnd")

		if (bndPropertiesFile.exists()) {
			InputStream inputStream = new FileInputStream(bndPropertiesFile)

			inputStream.withStream {
				bndProperties.load(it)
			}
		}

		// build.xml

		XmlParser xmlParser = new XmlParser()

		xmlParser.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false)

		File buildXmlFile = project.file("build.xml")

		if (buildXmlFile.exists()) {
			project.ext.buildXmlNode = xmlParser.parse(buildXmlFile)
		}

		// ivy.xml

		File ivyXmlFile = project.file("ivy.xml")

		if (ivyXmlFile.exists()) {
			project.ext.ivyXmlNode = xmlParser.parse(ivyXmlFile)
		}
		else {
			project.ext.ivyXmlNode = null
		}

		// liferay-plugin-package.properties

		project.ext.pluginPackageProperties = new Properties()

		File pluginPackagePropertiesFile = project.file("docroot/WEB-INF/liferay-plugin-package.properties")

		if (pluginPackagePropertiesFile.exists()) {
			InputStream inputStream = new FileInputStream(pluginPackagePropertiesFile)

			inputStream.withStream {
				pluginPackageProperties.load(it)
			}
		}
	}

	setExtProperties = {

		// osgiPlugin

		project.ext.osgiPlugin = project.exists("bnd.bnd")

		// pluginSrcDir

		project.ext.pluginSrcDir = project.file("docroot/WEB-INF/src")

		if (!project.pluginSrcDir.exists()) {
			project.pluginSrcDir = project.file("src")
		}
	}

	ext.getBuildXmlProperty = {
		String key, String defaultValue = "" ->

		String value = defaultValue

		Node buildXmlPropertyNode = buildXmlNode.property.find {
			it.@name == key
		}

		if (buildXmlPropertyNode) {
			value = buildXmlPropertyNode.@value
		}

		return value
	}

	readConfigurations()

	setExtProperties()

	apply from: "${gradle.lfrSdkDir}/build-plugins.gradle"
}

task compareDists

compareDists << {
	Closure compareDistFileContents
	Closure compareDistFileNames
	Closure compareDists
	Closure getFileChecksums

	List<String> antDistFileNames
	List<String> commonDistFileNames
	List<String> gradleDistFileNames

	compareDistFileContents = {
		for (String fileName : commonDistFileNames) {
			FileTree antZipFileTree = zipTree("dist/${fileName}")

			Map<String, String> antFileChecksums = getFileChecksums(antZipFileTree)

			FileTree gradleZipFileTree = zipTree("dist2/${fileName}")

			Map<String, String> gradleFileChecksums = getFileChecksums(gradleZipFileTree)

			Set<String> antFilePaths = antFileChecksums.keySet()
			Set<String> gradleFilePaths = gradleFileChecksums.keySet()

			Set<String> commonFilePaths = antFilePaths.intersect(gradleFilePaths)

			if ((antFilePaths.size() != commonFilePaths.size()) || (antFilePaths.size() != gradleFilePaths.size())) {
				println "${fileName} does not match."

				Set<String> uniqueFilePaths = antFilePaths - commonFilePaths

				if (uniqueFilePaths) {
					println "Unique files in the Ant distribution: " + uniqueFilePaths.join(", ") + "."
				}

				uniqueFilePaths = gradleFilePaths - commonFilePaths

				if (uniqueFilePaths) {
					println "Unique files in the Gradle distribution: " + uniqueFilePaths.join(", ") + "."
				}

				return false
			}

			List<String> differentChecksumFilePaths = antFileChecksums.findResults {
				filePath, antFileChecksum ->

				String gradleFileChecksum = gradleFileChecksums[filePath]

				if (antFileChecksum != gradleFileChecksum) {
					return filePath
				}

				return null
			}

			if (project.hasProperty("compareDistsExcludeSharedChecksums") && project.compareDistsExcludeSharedChecksums.toBoolean()) {
				differentChecksumFilePaths.removeAll {
					it.endsWith "-shared.jar"
				}
			}

			if (differentChecksumFilePaths) {
				println "${fileName} does not match and has different files: " + differentChecksumFilePaths.join(", ")

				return false
			}
		}

		return true
	}

	compareDistFileNames = {
		if ((antDistFileNames.size() != commonDistFileNames.size()) || (antDistFileNames.size() != gradleDistFileNames.size())) {
			println "Distribution directories contain different files."

			List<String> uniqueDistFileNames = antDistFileNames - commonDistFileNames

			if (uniqueDistFileNames) {
				println "Unique files in the Ant distribution directory: " + uniqueDistFileNames.join(", ") + "."
			}

			uniqueDistFileNames = gradleDistFileNames - commonDistFileNames

			if (uniqueDistFileNames) {
				println "Unique files in the Gradle distribution directory: " + uniqueDistFileNames.join(", ") + "."
			}

			return false
		}

		return true
	}

	compareDists = {
		FileTree antDistFileTree = fileTree(dir: "dist", include: "*.war")

		antDistFileNames = antDistFileTree*.name

		FileTree gradleDistFileTree = fileTree(dir: "dist2", include: "*.war")

		gradleDistFileNames = gradleDistFileTree*.name

		commonDistFileNames = antDistFileNames.intersect(gradleDistFileNames)

		if (!compareDistFileNames()) {
			return
		}

		if (!compareDistFileContents()) {
			return
		}

		println "Distribution directories do match."
	}

	getFileChecksums = {
		FileTree fileTree ->

		Map<String, String> fileChecksums = [:]

		fileTree.visit {
			FileVisitDetails fileVisitDetails ->

			if (fileVisitDetails.directory) {
				return
			}

			InputStream inputStream = fileVisitDetails.open()

			inputStream.withStream {
				String filePath = fileVisitDetails.relativePath
				String fileChecksum = DigestUtils.md5Hex(inputStream)

				fileChecksums[filePath] = fileChecksum
			}
		}

		return fileChecksums
	}

	compareDists()
}